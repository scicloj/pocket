---
format:
  html:
    toc: true
    toc-depth: 4
    theme: [cosmo, notebooks/custom.scss]
    toc-expand: 2
    number-depth: 1
    filters: [notebooks/collapse-callouts.lua]

---
<style></style><style>.printedClojure .sourceCode {
  background-color: transparent;
  border-style: none;
}
</style><style>.clay-limit-image-width .clay-image {max-width: 100%}
.clay-side-by-side .sourceCode {margin: 0}
.clay-side-by-side {margin: 1em 0}
</style>
<script src="https://code.jquery.com/jquery-3.6.0.min.js" type="text/javascript"></script><script src="https://code.jquery.com/ui/1.13.1/jquery-ui.min.js" type="text/javascript"></script>

# ðŸš§ Draft: `pocket-model` â€” drop-in caching for metamorph.ml

**Last modified: 2026-02-08**

This chapter shows how to cache model training in a
[metamorph.ml](https://github.com/scicloj/metamorph.ml) pipeline
using Pocket. We define a small `pocket-model` function â€” a
drop-in replacement for `ml/model` â€” and use it with
cross-validation, grid search, and multiple model types.


## Background

[metamorph.ml](https://github.com/scicloj/metamorph.ml) is the
Scicloj library for machine learning pipelines. It builds on
[metamorph](https://github.com/scicloj/metamorph), a
data-transformation framework where each step is a function that
takes a context map and returns an updated one. metamorph.ml
distinguishes two modes â€” `:fit` (learn from training data) and
`:transform` (apply to new data) â€” so a pipeline can be trained
once and reused for prediction.

On top of this, metamorph.ml adds model training/prediction,
cross-validation (`evaluate-pipelines`), loss functions, and
hyperparameter search. A typical workflow looks like:

1. Define a pipeline of preprocessing + model steps
2. Split data into folds
3. Call `evaluate-pipelines` to train and score across folds
4. Compare results, pick the best model


## Why cache with Pocket?

metamorph.ml includes a built-in caching mechanism. This notebook
explores what happens when we use Pocket's caching instead,
bringing a few things that are natural to Pocket's design:


- **Disk persistence** â€” cached models survive JVM restarts,
  so we can pick up where we left off across sessions

- **Content-based keys** â€” cache keys derived from function
  identity and full argument values via SHA-1

- **Concurrent dedup** â€” when multiple threads request the same
  computation, only one trains and the rest wait for the result

The integration is lightweight: a `pocket-model` function that
is a drop-in replacement for `ml/model`. We swap one pipeline
step and everything else â€” `evaluate-pipelines`, preprocessing,
grid search â€” stays the same.

**What this gives us**:

- Same pipeline code, same `evaluate-pipelines`
- Model training cached to disk (survives JVM restarts)
- Graceful fallback for non-serializable models


**What this notebook does not cover**: because `pocket-model`
plugs into metamorph.ml's existing pipeline machinery, only the
model-training step is cached through Pocket. Preprocessing,
splitting, and evaluation happen outside Pocket's awareness â€”
there is no computational DAG tracking the full pipeline, no
per-step storage control (choosing whether each step caches to
disk, memory, or not at all), and no provenance trail that
connects a final metric back to the data and parameters that
produced it.
A companion notebook is in the works, exploring a deeper
integration where every pipeline step is a Pocket `caching-fn`,
giving us all of those things.


## Setup


::: {.sourceClojure}
```clojure
(ns pocket-book.pocket-model
  (:require
   [pocket-book.logging]
   [scicloj.pocket :as pocket]
   [scicloj.kindly.v4.kind :as kind]
   [tablecloth.api :as tc]
   [tablecloth.column.api :as tcc]
   [tech.v3.dataset.modelling :as ds-mod]
   [tech.v3.dataset.column-filters :as cf]
   [scicloj.metamorph.ml :as ml]
   [scicloj.metamorph.ml.loss :as loss]
   [scicloj.metamorph.ml.regression]
   [scicloj.metamorph.core :as mm]
   [scicloj.ml.tribuo]))
```
:::



::: {.sourceClojure}
```clojure
(def cache-dir "/tmp/pocket-model")
```
:::



::: {.sourceClojure}
```clojure
(pocket/set-base-cache-dir! cache-dir)
```
:::



::: {.callout-note}
## OUT
```
19:24:45.223 INFO scicloj.pocket - Cache dir set to: /tmp/pocket-model

```
:::



::: {.printedClojure}
```clojure
"/tmp/pocket-model"

```
:::



::: {.sourceClojure}
```clojure
(pocket/cleanup!)
```
:::



::: {.callout-note}
## OUT
```
19:24:45.224 INFO scicloj.pocket - Cache cleanup: /tmp/pocket-model

```
:::



::: {.printedClojure}
```clojure
{:dir "/tmp/pocket-model", :existed false}

```
:::


---


## The `pocket-model` function

This is the core of the integration. It follows the same
contract as `ml/model` â€” a metamorph step that trains in
`:fit` mode and predicts in `:transform` mode. The only
difference: `ml/train` is wrapped with `pocket/cached`.

If Nippy can't serialize a model (e.g., Apache Commons Math
OLS), it falls back to uncached training automatically.


::: {.sourceClojure}
```clojure
(defn pocket-model
  "Drop-in replacement for ml/model that caches training via Pocket.
  Falls back to uncached training if serialization fails."
  [options]
  (fn [{:metamorph/keys [id data mode] :as ctx}]
    (case mode
      :fit
      (let [model (try
                    (deref (pocket/cached #'ml/train data options))
                    (catch Exception _e
                      (ml/train data options)))]
        (assoc ctx id (assoc model :scicloj.metamorph.ml/unsupervised?
                             (get (ml/options->model-def options)
                                  :unsupervised? false))))
      :transform
      (let [model (get ctx id)]
        (if (get model :scicloj.metamorph.ml/unsupervised?)
          ctx
          (-> ctx
              (update id assoc
                      :scicloj.metamorph.ml/feature-ds (cf/feature data)
                      :scicloj.metamorph.ml/target-ds (cf/target data))
              (assoc :metamorph/data (ml/predict data model))))))))
```
:::


---


## Test data

Simple synthetic regression: `y = 3x + noise`.
200 rows, enough for quick feedback.


::: {.sourceClojure}
```clojure
(def ds (-> (let [rng (java.util.Random. 42)]
              (tc/dataset
               {:x (vec (repeatedly 200 #(* 10.0 (.nextDouble rng))))
                :y (vec (repeatedly 200 #(+ (* 3.0 (* 10.0 (.nextDouble rng)))
                                            (* 2.0 (.nextGaussian rng)))))}))
            (ds-mod/set-inference-target :y)))
```
:::



::: {.sourceClojure}
```clojure
(def splits (tc/split->seq ds :kfold {:k 3 :seed 42}))
```
:::



::: {.sourceClojure}
```clojure
(count splits)
```
:::



::: {.printedClojure}
```clojure
3

```
:::


---


## Basic usage

Use `pocket-model` in place of `ml/model`.
The `{:metamorph/id :model}` map step sets the step ID
that `evaluate-pipelines` expects.


::: {.sourceClojure}
```clojure
(def cart-spec
  {:model-type :scicloj.ml.tribuo/regression
   :tribuo-components [{:name "cart"
                        :type "org.tribuo.regression.rtree.CARTRegressionTrainer"
                        :properties {:maxDepth "8"}}]
   :tribuo-trainer-name "cart"})
```
:::



::: {.sourceClojure}
```clojure
(def pipe-cart
  (mm/pipeline
   {:metamorph/id :model}
   (pocket-model cart-spec)))
```
:::


First run â€” trains 3 models (one per fold):


::: {.sourceClojure}
```clojure
(def results-1
  (ml/evaluate-pipelines
   [pipe-cart]
   splits
   loss/rmse
   :loss
   {:return-best-crossvalidation-only false
    :return-best-pipeline-only false}))
```
:::



::: {.callout-note}
## OUT
```
19:24:45.234 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.240 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.249 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train

```
:::



::: {.sourceClojure}
```clojure
(mapv #(-> % :test-transform :metric) (flatten results-1))
```
:::



::: {.printedClojure}
```clojure
[10.938693265902357 11.23113067170221 12.12978921023711]

```
:::


Cache now has 3 entries (one per fold):


::: {.sourceClojure}
```clojure
(pocket/cache-stats)
```
:::



::: {.printedClojure}
```clojure
{:total-entries 3,
 :total-size-bytes 53372,
 :entries-per-fn {"scicloj.metamorph.ml/train" 3}}

```
:::


Second run â€” all cache hits, same metrics:


::: {.sourceClojure}
```clojure
(def results-2
  (ml/evaluate-pipelines
   [pipe-cart]
   splits
   loss/rmse
   :loss
   {:return-best-crossvalidation-only false
    :return-best-pipeline-only false}))
```
:::



::: {.sourceClojure}
```clojure
(= (mapv #(-> % :test-transform :metric) (flatten results-1))
   (mapv #(-> % :test-transform :metric) (flatten results-2)))
```
:::



::: {.printedClojure}
```clojure
true

```
:::


---


## Incremental grid search

Start with 3 depth values, then add 3 more. Only new
combinations train â€” existing ones hit cache.


::: {.sourceClojure}
```clojure
(pocket/cleanup!)
```
:::



::: {.callout-note}
## OUT
```
19:24:45.277 INFO scicloj.pocket - Cache cleanup: /tmp/pocket-model

```
:::



::: {.printedClojure}
```clojure
{:dir "/tmp/pocket-model", :existed true}

```
:::



::: {.sourceClojure}
```clojure
(defn cart-pipe [max-depth]
  (mm/pipeline
   {:metamorph/id :model}
   (pocket-model
    {:model-type :scicloj.ml.tribuo/regression
     :tribuo-components [{:name "cart"
                          :type "org.tribuo.regression.rtree.CARTRegressionTrainer"
                          :properties {:maxDepth (str max-depth)}}]
     :tribuo-trainer-name "cart"})))
```
:::


Batch 1: depths 4, 8, 12


::: {.sourceClojure}
```clojure
(def batch-1
  (ml/evaluate-pipelines
   (mapv cart-pipe [4 8 12])
   splits
   loss/rmse
   :loss
   {:return-best-crossvalidation-only false
    :return-best-pipeline-only false}))
```
:::



::: {.callout-note}
## OUT
```
19:24:45.279 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.285 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.290 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.295 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.300 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.306 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.311 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.317 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.324 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train

```
:::


3 depths Ã— 3 folds = 9 trainings:


::: {.sourceClojure}
```clojure
(pocket/cache-stats)
```
:::



::: {.printedClojure}
```clojure
{:total-entries 9,
 :total-size-bytes 158766,
 :entries-per-fn {"scicloj.metamorph.ml/train" 9}}

```
:::


Batch 2: depths 4, 6, 8, 10, 12, 16
Depths 4, 8, 12 already cached â†’ only 6, 10, 16 are new


::: {.sourceClojure}
```clojure
(def batch-2
  (ml/evaluate-pipelines
   (mapv cart-pipe [4 6 8 10 12 16])
   splits
   loss/rmse
   :loss
   {:return-best-crossvalidation-only false
    :return-best-pipeline-only false}))
```
:::



::: {.callout-note}
## OUT
```
19:24:45.344 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.350 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.355 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.366 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.374 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.382 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.401 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.406 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.412 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train

```
:::


3 new depths Ã— 3 folds = 9 new + 9 cached = 18 total:


::: {.sourceClojure}
```clojure
(pocket/cache-stats)
```
:::



::: {.printedClojure}
```clojure
{:total-entries 18,
 :total-size-bytes 322358,
 :entries-per-fn {"scicloj.metamorph.ml/train" 18}}

```
:::


Combine results â€” best depth by mean RMSE:


::: {.sourceClojure}
```clojure
(let [depths [4 6 8 10 12 16]
      means (mapv (fn [pipeline-results]
                    (tcc/mean (map #(-> % :test-transform :metric) pipeline-results)))
                  batch-2)]
  (tc/dataset {:depth depths :mean-rmse means}))
```
:::


::: {.clay-dataset}
_unnamed [6 2]:

| :depth |  :mean-rmse |
|-------:|------------:|
|      4 | 12.89681757 |
|      6 | 12.36344906 |
|      8 | 12.28048327 |
|     10 | 11.43320438 |
|     12 | 10.68677435 |
|     16 |  9.86061013 |


:::


---


## Multiple model types

Compare CART, linear SGD, and fastmath OLS in the same
evaluation. Each model type is cached independently.


::: {.sourceClojure}
```clojure
(pocket/cleanup!)
```
:::



::: {.callout-note}
## OUT
```
19:24:45.434 INFO scicloj.pocket - Cache cleanup: /tmp/pocket-model

```
:::



::: {.printedClojure}
```clojure
{:dir "/tmp/pocket-model", :existed true}

```
:::



::: {.sourceClojure}
```clojure
(def sgd-spec
  {:model-type :scicloj.ml.tribuo/regression
   :tribuo-components [{:name "squared"
                        :type "org.tribuo.regression.sgd.objectives.SquaredLoss"}
                       {:name "trainer"
                        :type "org.tribuo.regression.sgd.linear.LinearSGDTrainer"
                        :properties {:objective "squared"
                                     :epochs "50"
                                     :loggingInterval "10000"}}]
   :tribuo-trainer-name "trainer"})
```
:::



::: {.sourceClojure}
```clojure
(def multi-results
  (ml/evaluate-pipelines
   [(mm/pipeline {:metamorph/id :model} (pocket-model cart-spec))
    (mm/pipeline {:metamorph/id :model} (pocket-model sgd-spec))
    (mm/pipeline {:metamorph/id :model} (pocket-model {:model-type :fastmath/ols}))]
   splits
   loss/rmse
   :loss
   {:return-best-crossvalidation-only false
    :return-best-pipeline-only false}))
```
:::



::: {.callout-note}
## OUT
```
19:24:45.435 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.441 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.446 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.451 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
Feb 08, 2026 7:24:45 PM org.tribuo.common.sgd.AbstractSGDTrainer train
INFO: Training SGD model with 133 examples
Feb 08, 2026 7:24:45 PM org.tribuo.common.sgd.AbstractSGDTrainer train
INFO: Outputs - RegressionInfo({name=y,id=0,count=133,max=32.285163,min=-3.003255,mean=15.591786,variance=84.043799})
19:24:45.457 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
Feb 08, 2026 7:24:45 PM org.tribuo.common.sgd.AbstractSGDTrainer train
INFO: Training SGD model with 133 examples
Feb 08, 2026 7:24:45 PM org.tribuo.common.sgd.AbstractSGDTrainer train
INFO: Outputs - RegressionInfo({name=y,id=0,count=133,max=31.652557,min=-1.736155,mean=15.631001,variance=80.557863})
19:24:45.462 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
Feb 08, 2026 7:24:45 PM org.tribuo.common.sgd.AbstractSGDTrainer train
INFO: Training SGD model with 134 examples
Feb 08, 2026 7:24:45 PM org.tribuo.common.sgd.AbstractSGDTrainer train
INFO: Outputs - RegressionInfo({name=y,id=0,count=134,max=32.285163,min=-3.003255,mean=16.262557,variance=77.697467})
19:24:45.468 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.477 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.482 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train

```
:::


3 model types Ã— 3 folds = 9 entries:


::: {.sourceClojure}
```clojure
(pocket/cache-stats)
```
:::



::: {.printedClojure}
```clojure
{:total-entries 9,
 :total-size-bytes 188806,
 :entries-per-fn {"scicloj.metamorph.ml/train" 9}}

```
:::


Mean RMSE per model type:


::: {.sourceClojure}
```clojure
(let [model-names ["CART" "SGD" "fastmath-OLS"]
      means (mapv (fn [pipeline-results]
                    (tcc/mean (map #(-> % :test-transform :metric) pipeline-results)))
                  multi-results)]
  (tc/dataset {:model model-names :mean-rmse means}))
```
:::


::: {.clay-dataset}
_unnamed [3 2]:

|       :model |  :mean-rmse |
|--------------|------------:|
|         CART | 11.43320438 |
|          SGD |  9.00886158 |
| fastmath-OLS |  9.01791979 |


:::


---


## Graceful fallback

The built-in `metamorph.ml/ols` uses Apache Commons Math
which Nippy can't serialize. `pocket-model` catches the
error and falls back to uncached training â€” the pipeline
still works, just without disk caching for that model.


::: {.sourceClojure}
```clojure
(pocket/cleanup!)
```
:::



::: {.callout-note}
## OUT
```
19:24:45.501 INFO scicloj.pocket - Cache cleanup: /tmp/pocket-model

```
:::



::: {.printedClojure}
```clojure
{:dir "/tmp/pocket-model", :existed true}

```
:::



::: {.sourceClojure}
```clojure
(def fallback-results
  (ml/evaluate-pipelines
   [(mm/pipeline {:metamorph/id :model} (pocket-model cart-spec))
    (mm/pipeline {:metamorph/id :model} (pocket-model {:model-type :metamorph.ml/ols}))]
   splits
   loss/rmse
   :loss
   {:return-best-crossvalidation-only false
    :return-best-pipeline-only false}))
```
:::



::: {.callout-note}
## OUT
```
19:24:45.502 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.510 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.517 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.523 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.530 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.536 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train

```
:::


CART models are cached â€” 3 entries, one per fold.
OLS falls back to uncached training silently. The failed
serialization attempts leave empty cache directories, which
show up as entries with a `nil` function name:


::: {.sourceClojure}
```clojure
(pocket/cache-stats)
```
:::



::: {.printedClojure}
```clojure
{:total-entries 6,
 :total-size-bytes 53369,
 :entries-per-fn {"scicloj.metamorph.ml/train" 3, nil 3}}

```
:::


Both model types produce valid metrics:


::: {.sourceClojure}
```clojure
(let [model-names ["CART" "OLS-fallback"]
      means (mapv (fn [pipeline-results]
                    (tcc/mean (map #(-> % :test-transform :metric) pipeline-results)))
                  fallback-results)]
  (tc/dataset {:model model-names :mean-rmse means}))
```
:::


::: {.clay-dataset}
_unnamed [2 2]:

|       :model |  :mean-rmse |
|--------------|------------:|
|         CART | 11.43320438 |
| OLS-fallback |  9.00886158 |


:::


---


## Disk persistence

Models survive JVM restarts. After clearing the in-memory
cache, models are loaded from disk on next access.


::: {.sourceClojure}
```clojure
(pocket/cleanup!)
```
:::



::: {.callout-note}
## OUT
```
19:24:45.551 INFO scicloj.pocket - Cache cleanup: /tmp/pocket-model

```
:::



::: {.printedClojure}
```clojure
{:dir "/tmp/pocket-model", :existed true}

```
:::


Train fresh:


::: {.sourceClojure}
```clojure
(def persist-results-1
  (ml/evaluate-pipelines
   [(mm/pipeline {:metamorph/id :model} (pocket-model cart-spec))]
   splits
   loss/rmse
   :loss
   {:return-best-crossvalidation-only false
    :return-best-pipeline-only false}))
```
:::



::: {.callout-note}
## OUT
```
19:24:45.552 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.559 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train
19:24:45.564 INFO scicloj.pocket.impl.cache - Cache miss, computing: scicloj.metamorph.ml/train

```
:::


Clear in-memory cache (simulates JVM restart):


::: {.sourceClojure}
```clojure
(pocket/clear-mem-cache!)
```
:::



::: {.printedClojure}
```clojure
{}

```
:::


Re-evaluate â€” loads from disk:


::: {.sourceClojure}
```clojure
(def persist-results-2
  (ml/evaluate-pipelines
   [(mm/pipeline {:metamorph/id :model} (pocket-model cart-spec))]
   splits
   loss/rmse
   :loss
   {:return-best-crossvalidation-only false
    :return-best-pipeline-only false}))
```
:::


Same metrics:


::: {.sourceClojure}
```clojure
(= (mapv #(-> % :test-transform :metric) (flatten persist-results-1))
   (mapv #(-> % :test-transform :metric) (flatten persist-results-2)))
```
:::



::: {.printedClojure}
```clojure
true

```
:::


---


## Discussion

`pocket-model` is a thin wrapper â€” about 20 lines of code â€” that
gives us disk-persistent model caching with zero changes to our
pipeline structure. It works with `evaluate-pipelines`,
preprocessing steps, learning curves, and grid search.

**Serialization compatibility** (tested):

| Backend | Cacheable? |
|---------|-----------|
| Tribuo regression (CART, SGD) | Yes |
| Tribuo classification | Yes |
| fastmath/ols | Yes |
| metamorph.ml/ols (Commons Math) | No (falls back) |
| metamorph.ml/dummy-regressor | Yes |

**When to use `pocket-model`**:

- Grid search / hyperparameter tuning (train once, reuse)
- Iterative notebook development (change downstream code, keep models)
- Learning curves (add new sizes, only new ones train)
- Any workflow where we re-evaluate with the same data + options


## Cleanup


::: {.sourceClojure}
```clojure
(pocket/cleanup!)
```
:::



::: {.callout-note}
## OUT
```
19:24:45.587 INFO scicloj.pocket - Cache cleanup: /tmp/pocket-model

```
:::



::: {.printedClojure}
```clojure
{:dir "/tmp/pocket-model", :existed true}

```
:::



```{=html}
<div style="background-color:grey;height:2px;width:100%;"></div>
```



```{=html}
<div><pre><small><small>source: <a href="https://github.com/scicloj/pocket/blob/master/notebooks/pocket_book/pocket_model.clj">notebooks/pocket_book/pocket_model.clj</a></small></small></pre></div>
```